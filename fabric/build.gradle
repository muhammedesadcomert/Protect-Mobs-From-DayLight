plugins {
    id 'fabric-loom' version '1.1-SNAPSHOT'
    id 'java'
}

repositories {
    maven {
        name 'ModMenu'
        url 'https://maven.terraformersmc.com/'
    }
}

archivesBaseName = "$archivesBaseName-fabric"

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"
    implementation project(":common")
}

loom {
    mixin.defaultRefmapName = "fabric.refmap.json"
}

tasks.register("replaceResources", Copy) {
    outputs.upToDateWhen { false }

    from(sourceSets.main.resources) {
        include 'fabric.mod.json'
        expand id: mod_id,
                version: mod_version,
                name: mod_name,
                description: mod_description,
                authors: mod_authors,
                homepage: mod_homepage,
                issues: mod_issues,
                license: mod_license,
                icon: mod_icon,
                fabric_loader: fabric_loader_version,
                minecraft: minecraft_version
    }

    from(project(":common").sourceSets.main.resources) {
        include 'common.mixins.json', 'pack.mcmeta'
        expand refmap_json: "fabric.refmap.json",
                mod_name: mod_name
        rename 'common.mixins.json', 'fabric.mixins.json'
    }

    into "$buildDir/resources/main/"
}

processResources {
    from(project(":common").sourceSets.main.resources)
    duplicatesStrategy(DuplicatesStrategy.FAIL)
    exclude 'mods.toml'
    finalizedBy replaceResources
}

tasks.withType(JavaCompile).configureEach {
    source(project(":common").sourceSets.main.allSource)
}

tasks.jar.dependsOn replaceResources
tasks.compileTestJava.dependsOn replaceResources

jar {
    exclude 'common.mixins.json'

    manifest {
        attributes([
                "Specification-Title"     : mod_id,
                "Specification-Vendor"    : mod_authors,
                "Specification-Version"   : project.version,
                "Implementation-Title"    : mod_name,
                "Implementation-Version"  : project.version,
                "Implementation-Vendor"   : mod_authors,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}